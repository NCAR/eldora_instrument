import os
import eol_scons

def Eldora(env):
    # define ELDORA_ROOT as a double-quote-delimited string containing the 
    # top-level directory of the Eldora build.  E.g., this will add something
    # like -DELDORA_ROOT='"/code/burghart/eldora"' to each C/C++ compilation.
    env.AppendUnique(CPPDEFINES=['ELDORA_ROOT=' + 
                                 '\'"' + env.Dir('#').get_abspath() + '"\''])
    if env['CC'] == "gcc":
        env.AppendUnique(_LIBFLAGS=['-lpthread'])
        env.AppendUnique(CCFLAGS=['-fmessage-length=0'])
    else:
        env.AppendUnique(CCFLAGS=['/EHsc','/MDd','/GR','/GX'])
        env.PassEnv(r'VC.*')
        env.PassEnv(r'VS.*')
        env.PassEnv(r'DevEnvDir')
        env.PassEnv(r'Framework.*')

tools = ['sdrscope','disttar']
env = Environment(tools = ['default'] + tools, GLOBAL_TOOLS = [Eldora])

Export('env')


# Build the main eldora applications:
SConscript('conf/SConscript')
SConscript('designer/SConscript')
SConscript('eldorappi/SConscript')
SConscript('eldoraprod/SConscript')
SConscript('eldorascope/SConscript')
SConscript('eldoragui/SConscript')
SConscript('headermaker/SConscript')
SConscript('dorade/tool_dorade.py')

# The eldoradrx program
SConscript('eldoradrx/SConscript')

# The qttoobox support
SConscript('qttoolbox/SConscript')

# Test apps for the utilities
SConscript('utilities/test/SConscript')

# A DDS test harness
SConscript('testdds/SConscript')

# ECB
SConscript('ecb/SConscript')

# VxWorks (the housekeeper)
SConscript('vx-eldora/SConscript')

options = env.GlobalOptions()
options.Update(env)
Help(options.GenerateHelpText(env))

py_site ='/usr/lib/python2.4/site-packages/'
env['DISTTAR_PREFIX'] ='/usr/lib'
pdist = env.DistTar("dist/py_site", env.Dir(py_site))
env.Default(pdist)
env.Alias('install', pdist)

distDir = '/opt/eldora'
env['DISTTAR_PREFIX'] ='/opt'
dist = env.DistTar("dist/eldora", env.Dir(distDir))
env.Default(dist)
env.Alias('install', dist)


