import os
import sys
import eol_scons

if not os.environ.has_key('ELDORADIR'):
    os.environ['ELDORADIR'] = '/opt/eldora'
    print "ELDORADIR environment variable is not set."
    print "Installation will go to default location:", os.environ['ELDORADIR']
    
ELDORADIR = os.environ['ELDORADIR']

def Eldora(env):
    env['DEFAULT_INSTALL_PREFIX'] = ELDORADIR

    # recommended options from http://scons.org/wiki/GoFastButton
    env.Decider('MD5-timestamp')
    env.SourceCode('.',None)
    env.SetOption('max_drift', 5)
    if env['CC'] == "gcc":
        env.AppendUnique(_LIBFLAGS=['-lpthread'])
        env.AppendUnique(CCFLAGS=['-fmessage-length=0'])
    else:
        env.AppendUnique(CCFLAGS=['/EHsc','/MDd','/GR','/GX'])
        env.PassEnv(r'VC.*')
        env.PassEnv(r'VS.*')
        env.PassEnv(r'DevEnvDir')
        env.PassEnv(r'Framework.*')

tools = ['sdrscope', 'disttar']
env = Environment(tools = ['default'] + tools, GLOBAL_TOOLS = [Eldora])

# Build the main eldora applications:
SConscript('conf/SConscript')
SConscript('designer/SConscript')
SConscript('eldorappi/SConscript')
SConscript('eldoraprod/SConscript')
SConscript('eldorascope/SConscript')
SConscript('eldoragui/SConscript')
SConscript('headermaker/SConscript')
SConscript('dorade/tool_dorade.py')

# The eldoradrx program
SConscript('eldoradrx/SConscript')

# Test apps for the utilities
SConscript('utilities/test/SConscript')

# A DDS test harness
SConscript('testdds/SConscript')

# ECB
SConscript('ecb/SConscript')

# radd archiver stuff
SConscript('acex/SConscript')
SConscript('logx/SConscript')
SConscript('domx/SConscript')
SConscript('rtfcommon/SConscript')
SConscript('radd/archiver/SConscript')
SConscript('radd/archiver-servant/SConscript')

# Add an install target for archiver-servant. Since it belongs to an
# external, we don't want to munge the SConscript there just for 
# this purpose.
env.Alias('install', 
          env.InstallProgram('radd/archiver-servant/archiver-servant'))

# EldoraArchiver
#SConscript('eldoraarchiver/SConscript')

# VxWorks (the housekeeper)
SConscript('vx-eldora/SConscript')

options = env.GlobalOptions()
options.Update(env)
Help(options.GenerateHelpText(env))

# Build the official site-packages path for the Python we're using.
# Typically, this will be something like '/usr/lib/python2.4/site-packages'
py_site = os.path.join(sys.exec_prefix, 'lib', 'python' + sys.version[:3], 
                       'site-packages')
pdist = env.DistTar("dist/py_site", env.Dir(py_site), DISTTAR_PREFIX ='/usr/lib')
env.Alias('distrib', pdist)

distDir = ELDORADIR
parent = os.path.abspath(os.path.join(distDir, '..'))
dist = env.DistTar("dist/eldora", env.Dir(distDir), DISTTAR_PREFIX = parent)
env.Alias('distrib', dist)


