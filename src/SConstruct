import os
import eol_scons

if not os.environ.has_key('ELDORADIR'):
    os.environ['ELDORADIR'] = '/opt/eldora'
    print "ELDORADIR environment variable is not set."
    print "Installation will go to default location:", os.environ['ELDORADIR']
    
ELDORADIR = os.environ['ELDORADIR']

def Eldora(env):
    # define ELDORA_ROOT as a double-quote-delimited string containing the 
    # top-level directory of the Eldora build.  E.g., this will add something
    # like -DELDORA_ROOT='"/code/burghart/eldora"' to each C/C++ compilation.
    env['DEFAULT_INSTALL_PREFIX'] = ELDORADIR
    env.AppendUnique(CPPDEFINES=['ELDORA_ROOT=' + 
                                 '\'"' + env.Dir('#').get_abspath() + '"\''])
    # recommended options from http://scons.org/wiki/GoFastButton
    env.Decider('MD5-timestamp')
    env.SourceCode('.',None)
    if env['CC'] == "gcc":
        env.AppendUnique(_LIBFLAGS=['-lpthread'])
        env.AppendUnique(CCFLAGS=['-fmessage-length=0'])
    else:
        env.AppendUnique(CCFLAGS=['/EHsc','/MDd','/GR','/GX'])
        env.PassEnv(r'VC.*')
        env.PassEnv(r'VS.*')
        env.PassEnv(r'DevEnvDir')
        env.PassEnv(r'Framework.*')

tools = ['sdrscope', 'disttar']
env = Environment(tools = ['default'] + tools, GLOBAL_TOOLS = [Eldora])

# Build the main eldora applications:
SConscript('conf/SConscript')
SConscript('designer/SConscript')
SConscript('eldorappi/SConscript')
SConscript('eldoraprod/SConscript')
SConscript('eldorascope/SConscript')
SConscript('eldoragui/SConscript')
SConscript('headermaker/SConscript')
SConscript('dorade/tool_dorade.py')

# The eldoradrx program
SConscript('eldoradrx/SConscript')

# The qttoobox support
SConscript('qttoolbox/SConscript')

# Test apps for the utilities
SConscript('utilities/test/SConscript')

# A DDS test harness
SConscript('testdds/SConscript')

# ECB
SConscript('ecb/SConscript')

# VxWorks (the housekeeper)
SConscript('vx-eldora/SConscript')

options = env.GlobalOptions()
options.Update(env)
Help(options.GenerateHelpText(env))

py_site ='/usr/lib/python2.4/site-packages/'
pdist = env.DistTar("dist/py_site", env.Dir(py_site), DISTTAR_PREFIX ='/usr/lib')
#env.Default(pdist)
env.Alias('distrib', pdist)

distDir = ELDORADIR
parent = os.path.abspath(os.path.join(distDir, '..'))
dist = env.DistTar("dist/eldora", env.Dir(distDir), DISTTAR_PREFIX = parent)
#env.Default(dist)
env.Alias('distrib', dist)


